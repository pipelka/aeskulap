AC_PREREQ(2.52)

AC_INIT(aeskulap, 0.2.1, pipelka@teleweb.at, aeskulap)
AM_INIT_AUTOMAKE([subdir-objects])
dnl AM_MAINTAINER_MODE

AM_CONFIG_HEADER(config.h)

dnl
dnl Require programs
dnl
AC_PROG_INTLTOOL(0.33)
AC_PROG_LIBTOOL
AC_PROG_CC
AM_PROG_CC_C_O
AC_PROG_CXX
AC_PROG_INSTALL
AM_BINRELOC

ALL_LINGUAS="en de es hu pt"
AM_GNU_GETTEXT
GETTEXT_PACKAGE="aeskulap"
AC_SUBST(GETTEXT_PACKAGE)

dnl
dnl Required headers
dnl
AC_HEADER_STDC

dnl
dnl Checks for typedefs, structures, and compiler characteristics.
dnl
AC_TYPE_MODE_T
AC_TYPE_PID_T
AC_HEADER_TIME

AC_CHECK_LIB(jpeg,jpeg_read_header)
AC_CHECK_LIB(tiff,TIFFClose)
AC_CHECK_LIB(png,png_create_read_struct)

AM_GNU_GETTEXT_VERSION(0.14.1)

PKG_CHECK_MODULES(GTKMM, gtkmm-2.4 >= 2.6.0)
AC_SUBST(GTKMM_CFLAGS)
AC_SUBST(GTKMM_LIBS)

PKG_CHECK_MODULES(GLADEMM, libglademm-2.4 >= 2.6.0)
AC_SUBST(GLADEMM_CFLAGS)
AC_SUBST(GLADEMM_LIBS)

PKG_CHECK_MODULES(GTHREAD, gthread-2.0)
AC_SUBST(GTHREAD_CFLAGS)
AC_SUBST(GTHREAD_LIBS)

PKG_CHECK_MODULES(GCONFMM, gconfmm-2.6 >= 2.10.0)
AC_SUBST(GCONFMM_CFLAGS)
AC_SUBST(GCFONMM_LIBS)

AC_ARG_ENABLE(jpeg2000,
  [  --enable-jpeg2000       enable jpeg2000 support],
  [use_jpeg2000=$enableval], [use_jpeg2000=no])

if test "x$use_jpeg2000" = "xyes"; then
    have_jasper_lib=no
    have_jasper_include=no

    AC_CHECK_LIB(jasper, jas_init, have_jasper_lib=yes)
    AC_CHECK_HEADER(jasper/jasper.h, [have_jasper_include=yes])
    
    if test "x$have_jasper_include" = "xno" ; then
	AC_CHECK_HEADER(jasper.h, [have_jasper_include=yes])
    fi

    if test "x$have_jasper_lib" = "xno" ; then
	use_jpeg2000=no;
    fi

    if test "x$have_jasper_include" = "xno" ; then
	use_jpeg2000=no;
    fi

fi

if test "x$use_jpeg2000" = "xyes"; then
    AC_DEFINE([ENABLE_JPEG2000], , [Define to 1 if JPEG2000 support is enabled.])
    DCMJP2DIR="dcmjp2"
    DCMJP2_LIBS="../dcmjp2/libsrc/libdcmjp2.la -ljasper"
fi

AC_SUBST(DCMJP2DIR)
AC_SUBST(DCMJP2_LIBS)


dnl AC_CONFIG_SUBDIRS(dcmtk)

DCMJPEG_LIBS="-L../dcmtk/dcmjpeg/libijg8 -L../dcmtk/dcmjpeg/libijg12 -L../dcmtk/dcmjpeg/libijg16 -lijg8 -lijg12 -lijg16"
AC_SUBST(DCMJPEG_LIBS)

DCMTK_CFLAGS="-DHAVE_CONFIG_H -I\$(top_srcdir)/dcmtk/config/include -I\$(top_srcdir)/dcmtk/ofstd/include -I\$(top_srcdir)/dcmtk/dcmdata/include -I\$(top_srcdir)/dcmtk/dcmnet/include -I\$(top_srcdir)/dcmtk/dcmimage/include -I\$(top_srcdir)/dcmtk/dcmimgle/include -I\$(top_srcdir)/dcmtk/dcmjpeg/include"
DCMTK_LIBS="-L../dcmtk/ofstd/libsrc \
-L../dcmtk/dcmdata/libsrc \
-L../dcmtk/dcmnet/libsrc \
-L../dcmtk/dcmjpeg/libsrc \
-L../dcmtk/dcmimage/libsrc \
-L../dcmtk/dcmimgle/libsrc \
-ldcmjpeg \$(DCMJPEG_LIBS) -ldcmimage -ldcmimgle -ldcmnet -ldcmdata -lofstd -lrt -lpthread"

AC_SUBST(DCMTK_CFLAGS)
AC_SUBST(DCMTK_LIBS)

dnl set PREFIX
if test "x${prefix}" = "xNONE"; then
    packageprefix=${ac_default_prefix}
else
    packageprefix=${prefix}
fi

dnl images directory
aeskulap_images_dir="${packageprefix}/share/aeskulap/images"
AC_SUBST(aeskulap_images_dir)

dnl AC_CONFIG_SUBDIRS(dcmtk)

AC_OUTPUT([
autopackage/default.apspec
intl/Makefile
po/Makefile.in
dcmjp2/Makefile
dcmjp2/include/Makefile
dcmjp2/libsrc/Makefile
Makefile
displaylist/Makefile
imagepool/Makefile
src/Makefile
widgets/Makefile
pixmaps/Makefile
])

cd dcmtk
export CFLAGS="$CFLAGS"
export CXXFLAGS="$CXXFLAGS"
sh ./configure --without-openssl --without-zlib --without-libwrap --prefix=${prefix} --libdir=${prefix}/lib/aeskulap
